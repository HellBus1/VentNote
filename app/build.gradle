import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.dagger.hilt.android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.2.10'
    id 'org.jetbrains.kotlin.plugin.compose' version '2.2.10'
}

// Apply Google Services and Crashlytics only if google-services.json exists
if (file('google-services.json').exists()) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    println 'Google Services and Crashlytics plugins applied'
} else {
    println 'google-services.json not found. Skipping Google Services and Crashlytics plugins'
}

android {
    namespace 'com.digiventure.ventnote'
    compileSdk = 36

    defaultConfig {
        applicationId "com.digiventure.ventnote"
        minSdk 23
        targetSdk = 36
        versionCode 47
        versionName "1.2.0"

        testInstrumentationRunner "com.digiventure.utils.CustomTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "true"
        }
        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_CRASHLYTICS", "false"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(JvmTarget.JVM_17)
        }
    }

    buildFeatures {
        compose true
        buildConfig true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.15'
    }


    packaging {
        resources {
            excludes.add('/META-INF/{AL2.0,LGPL2.1}')
            excludes.add('META-INF/DEPENDENCIES')
            excludes.add('META-INF/LICENSE')
            excludes.add('META-INF/LICENSE.txt')
            excludes.add('META-INF/license.txt')
            excludes.add('META-INF/NOTICE')
            excludes.add('META-INF/NOTICE.txt')
            excludes.add('META-INF/notice.txt')
            excludes.add('META-INF/ASL2.0')
            excludes.add('META-INF/*.kotlin_module')
            excludes.add('META-INF/services/org.jetbrains.kotlin.*')
        }
    }

    lint {
        abortOnError false
    }

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}


dependencies {
    implementation "androidx.core:core-ktx:$core_ktx_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_ktx_version"

    implementation "androidx.activity:activity-compose:$activity_compose_version"

    // Jetpack Compose
    implementation "androidx.compose.runtime:runtime-livedata:$jetpack_compose_version"
    implementation "androidx.compose.ui:ui:$jetpack_compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$jetpack_compose_version"

    implementation "androidx.compose.material3:material3:$material3_version"

    // Lifecycle Livedata
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_ktx_version"

    implementation "androidx.compose.compiler:compiler:$compose_compiler_version"

    // Room
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Datastore
    implementation "androidx.datastore:datastore-preferences:$datastore_preference_version"

    // Compose Navigation
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.8.0"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    // Hilt
    implementation "com.google.dagger:hilt-android:$dagger_version"
    kapt "com.google.dagger:hilt-android-compiler:$dagger_version"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_navigation_compose_version"

    // So, make sure you also include that repository in your project's build.gradle file.
    implementation "com.google.android.play:app-update:$play_app_update_version"
    // For Kotlin users also import the Kotlin extensions library for Play In-App Update:
    implementation "com.google.android.play:app-update-ktx:$play_app_update_version"

    // Google Play API
    implementation "com.google.android.gms:play-services-auth:$play_services_auth_version"

    // Accompanist - Status Bar
    implementation "com.google.accompanist:accompanist-systemuicontroller:$systemuicontroller_version"

    // Unit test
    testImplementation "junit:junit:$junit_version"

    // Instrumented test
    /// Espresso (for ui interaction purpose)
    androidTestImplementation "androidx.test:runner:$test_version"
    androidTestImplementation "androidx.test:rules:$test_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$test_espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"

    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_test_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_test_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_test_version"

    /// Hilt test (for handling service locator when test)
    androidTestImplementation "com.google.dagger:hilt-android-testing:$dagger_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$dagger_version"

    // For mocking purposes & make it visible in instrumented test
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockito_version"
    testImplementation "org.mockito:mockito-inline:$mockito_inline_version"

    androidTestImplementation "androidx.test.ext:junit-ktx:$junit_ktx_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-serialization-core:1.8.0"

    testImplementation "androidx.arch.core:core-testing:$core_testing_version"

    // For unit testing coroutines
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_test_version"

    // Import the Firebase BoM
    implementation platform("com.google.firebase:firebase-bom:$firebase_bom_version")
    // When using the BoM, you don't specify versions in Firebase library dependencies
    // Add the dependency for the Firebase SDK for Google Analytics
    implementation "com.google.firebase:firebase-crashlytics"

    //Google Drive API
    implementation "com.google.http-client:google-http-client-gson:$http_client_gson_version"
    implementation "com.google.apis:google-api-services-drive:$google_drive_service_version"
    implementation "com.google.api-client:google-api-client-android:$google_api_client_version"

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}

kapt {
    correctErrorTypes = true
}